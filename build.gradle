buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id "com.dorongold.task-tree" version "1.4"
}

group 'qilin'
// version format: major.minor.patch
version '0.9.7-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(':qilin.pta'))
    implementation(project(':qilin.core'))
    implementation(project(':qilin.util'))
    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

tasks.register('fatJar', Jar) {
    dependsOn(tasks.named('build'))
    manifest {
        attributes 'artifact': 'qilin',
                'Version': getArchiveVersion(),
                'Main-Class': 'driver.Main'
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from "${layout.buildDirectory.get()}/libs/${archiveBaseName.get()}-${getArchiveVersion().get()}.jar"
            into "$rootDir/artifact/"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'

    group rootProject.group
    version rootProject.version

    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from(sourceSets.main.allSource)
    }

    tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
        from(javadoc.destinationDir)
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
            // vendor = JvmVendorSpec.ADOPTIUM
        }
    }
    publishing.publications {
        qilin(MavenPublication) {
            artifactId = artifactId.substring(6)
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    publishing.repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/QiLinPTA/QiLin"
            credentials {
                username = project.findProperty("gpr.user") ?: "QiLinPTA"
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}
